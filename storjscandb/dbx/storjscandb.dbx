model block_header (
	key hash

	field hash       blob
	field number     int64
	field timestamp  timestamp
	field created_at timestamp ( autoinsert, default current_timestamp )
)

create block_header ( )
delete block_header ( where block_header.hash = ? )

read all (
	select block_header
	orderby desc block_header.timestamp
)

read one (
	select block_header
	where block_header.hash = ?
)

read one (
	select block_header
	where block_header.number = ?
)

model token_price (
	key interval_start

	field interval_start timestamp
	field price          float64   ( updatable )
)

create token_price (
	noreturn
	replace
)
delete token_price ( where token_price.interval_start = ? )

read one (
	select token_price
	where token_price.interval_start = ?
)

read first (
	select token_price
	where token_price.interval_start > ?
	orderby asc token_price.interval_start
)

model wallet (
	key address

	field address    text
	field claimed    timestamp ( updatable, nullable )
	field satellite  text      ( updatable, nullable )
	field info       text      ( updatable, nullable )
	field created_at timestamp ( autoinsert, default current_timestamp )

	index ( fields satellite )
)

create wallet ( )
update wallet ( where wallet.address = ? )

read one (
	select wallet
	where wallet.address = ?
)

read first (
	select wallet
	where wallet.claimed = null
)

read count (
	select wallet.address
)

read count (
	select wallet
	where wallet.claimed != null
)

read count (
	select wallet
	where wallet.claimed = null
)

read all (
	select wallet
	where wallet.satellite = ?
)
