//--- block_header ---//
model block_header (
	key hash

	index (
		name block_header_timestamp
		fields timestamp
	)

	field hash       blob
	field number     int64
	field timestamp  timestamp
	field created_at timestamp ( autoinsert, default current_timestamp )
)

create block_header ( )
delete block_header ( where block_header.hash = ? )

read all (
	select block_header
	orderby desc block_header.timestamp
)

read one (
	select block_header
	where block_header.hash = ?
)

read one (
	select block_header
	where block_header.number = ?
)

read first (
	select block_header
	where block_header.timestamp > ?
)

//--- token price ---//
model token_price (
    key interval_start

    field interval_start timestamp
    field price          float64   ( updatable )
)

create token_price ( )
update token_price ( where token_price.interval_start = ? )
delete token_price ( where token_price.interval_start = ? )

read one (
	select token_price
	where token_price.interval_start = ?
)

read first (
	select token_price
	where token_price.interval_start > ?
	orderby asc token_price.interval_start
)

//--- wallet ---//
model wallet (
	key address

	field address text
	field claimed timestamp (updatable, nullable)
	field apikey blob
	field info text (updatable, nullable) // can be used to store derivation path
	field created_at timestamp ( autoinsert, default current_timestamp )
)

create wallet ( )
update wallet ( where wallet.address = ?)

read one (
	select wallet
	where wallet.address = ?
)

read first (
	select wallet
	where wallet.claimed = null
)

read count (
	select wallet.address
)

read count (
	select wallet
	where wallet.claimed != null
)

read count (
	select wallet
	where wallet.claimed = null
)
